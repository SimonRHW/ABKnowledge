import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'simon.asm.plugin'
apply from: "$project.rootDir/jacoco.gradle"

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {

    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        applicationId "com.simon.basic.knowledge"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk

        versionCode project.VERSIONCODE as int
        versionName project.VERSIONNAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        buildConfigField("String", "GIT_VERSION", "\"${getGitVersion()}\"")
    }

    // The default value for each feature is shown below. You can change the value to
    // override the default behavior.
    buildFeatures {
        // Determines whether to generate a BuildConfig class.
        //buildConfig = true
        // Determines whether to support View Binding.
        // Note that the viewBinding.enabled property is now deprecated.
        viewBinding = true
        // Determines whether to support Data Binding.
        // Note that the dataBinding.enabled property is now deprecated.
        dataBinding = true
        // Determines whether to generate binder classes for your AIDL files.
        //aidl = true
        // Determines whether to support RenderScript.
        //renderScript = true
        // Determines whether to support injecting custom variables into the module’s R class.
        //resValues = true
        // Determines whether to support shader AOT compilation.
        //shaders = true
    }

    androidExtensions {
        experimental = true
    }

    signingConfigs {
        release {
            keyAlias project.KEY_ALIAS
            keyPassword project.KEY_PASSWORD
            storeFile file(project.STORE_FILE)
            storePassword project.STORE_PASSWORD
        }
        debug {
            keyAlias project.KEY_ALIAS
            keyPassword project.KEY_PASSWORD
            storeFile file(project.STORE_FILE)
            storePassword project.STORE_PASSWORD
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = applicationId + "_" + variant.buildType.name + "_v" + defaultConfig.versionName + "_" + versionTime() + ".apk"
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.debug
            debuggable true
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }

        release {
            debuggable false
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {

        //联测、开发环境
        dev {
            dimension "default"
            applicationId 'com.simon.basic.knowledge'
            buildConfigField("String", "host", "\"https://dev.com/\"")
            manifestPlaceholders = [
                    APP_NAME            : "@string/app_name",
                    APP_ICON            : "@mipmap/ic_launcher",
                    SHARED_USER_ID_VALUE: "com.simon"
            ]
        }

//        //测试环境
//        qa {
//            buildConfigField("String", "host", "\"https://qa.com/\"")
//        }
//
//        //预发布环境
//        approval {
//            buildConfigField("String", "host", "\"https://approval.com/\"")
//        }
//
//        //线上生产环境
//        live {
//            buildConfigField("String", "host", "\"https://live.com/\"")
//        }
    }

    testOptions {
        unitTests {
            compileSdkVersion build_versions.compile_sdk
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
            systemProperty 'robolectric.enabledSdks', build_versions.compile_sdk
            jvmArgs '-noverify'
        }

    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.javaVersion
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':logLib')
    implementation project(':basecore')
    implementation project(path: ':imagecore')
    implementation project(path: ':netcore')

    if (!rootProject.ext.newsRunAlone) {
        implementation project(path: ':module-news')
    }

    implementation deps.app_compat
    implementation deps.material
    implementation deps.core_ktx
    implementation deps.constraint_layout
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    implementation deps.lifecycle.viewmodel_ktx
    implementation deps.lifecycle.livedata_ktx
    implementation deps.activity.activity_ktx


    // Navigation
    implementation deps.navigation.runtime_ktx
    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx

    implementation deps.kotlin.stdlib

    //ARouter
    implementation deps.arouter.api

    kapt deps.arouter.compiler

    //Hilt
    implementation deps.hilt.runtime
    kapt deps.hilt.compiler

    testImplementation deps.junit
    testImplementation deps.arch_core.testing

    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.atsl.ext_junit
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.contrib
    androidTestImplementation deps.arch_core.testing
}

static def versionTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

static String getDate() {
    Date date = new Date()
    return new SimpleDateFormat("MMddHHmm", Locale.CHINA).format(date)
}

def getGitVersion() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}
