// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'versions.gradle'
    addRepos(repositories)
    dependencies {
        classpath deps.android_gradle_plugin
        classpath deps.kotlin.plugin
        classpath deps.kotlin.allopen
        classpath deps.hilt.plugin
        classpath deps.jacoco
        classpath deps.sonar
        //加载自定义插件
        //classpath 'com.simon.asm.core:asm_plugin:1.0.0'
    }
}
apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property 'sonar.projectName', 'ABKnowledge'
        property "sonar.projectKey", "com.simonren.basic.knowledge"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.exclusions", [
                '**/view/*'
        ]
    }
}

project(":common:core") {
    sonarqube {
        skipProject = true
    }
}

project(":library:logger") {
    sonarqube {
        skipProject = true
    }
}

subprojects {
    sonarqube {
        properties {
            property "sonar.sources", [
                    "src/main/java"
            ]
            property "sonar.tests", "src/test/unit/java"
            property "sonar.core.codeCoveragePlugin", "jacoco"
            property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/jacocoReport/jacocoTestProdDebugUnitTestReport/jacocoTestProdDebugUnitTestReport.xml"
        }
    }
}

allprojects {
    repositories {
        addRepos(repositories)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
